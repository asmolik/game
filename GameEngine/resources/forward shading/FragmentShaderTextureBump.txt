#version 330

in vec4 vertexNormal;
in vec4 vertexTangent;
in vec4 worldPos;
in vec2 texCo;

out vec4 outputColor;

uniform sampler2D normalSampler;
uniform sampler2D diffuseColorSampler;
uniform sampler2D specularColorSampler;
uniform float shininessFactor;

uniform vec4 ambientColor;
uniform vec4 sunColor;
uniform vec4 sunDirection;
uniform float maxIntensity;

uniform vec4 cameraPos;


vec3 calcBumpedNormal()
{
    vec3 normal = normalize(vertexNormal.xyz);
	vec3 tangent = normalize(vertexTangent.xyz);
    tangent = normalize(tangent - dot(tangent, normal) * normal);
    vec3 bitangent = cross(tangent, normal);
    vec3 bumpMapNormal = texture(normalSampler, texCo).xyz;
    bumpMapNormal = 2.0 * bumpMapNormal - vec3(1.0, 1.0, 1.0);
    vec3 newNormal;
    mat3 TBN = mat3(tangent, bitangent, normal);
    newNormal = TBN * bumpMapNormal;
    newNormal = normalize(newNormal);
    return newNormal;
}

void main()
{
	vec4 diffuseColor = texture(diffuseColorSampler, texCo);
	vec4 specularColor = texture(specularColorSampler, texCo);
	vec4 surfaceNormal = vec4(calcBumpedNormal(), 0.0);

	float cosAng = dot(surfaceNormal, -sunDirection);
	cosAng = clamp(cosAng, 0, 1);

	vec4 viewDirection = normalize(cameraPos - worldPos);

	vec4 halfAngle = normalize(-sunDirection + viewDirection);
	float angleNormalHalf = acos(dot(halfAngle, surfaceNormal));
	float exponent = angleNormalHalf / shininessFactor;
	exponent = -(exponent * exponent);
	float gaussianTerm = exp(exponent);

	gaussianTerm = cosAng != 0.0 ? gaussianTerm : 0.0;

	outputColor = (diffuseColor * ambientColor) + 
		(diffuseColor * sunColor * cosAng) + 
		(specularColor * sunColor * gaussianTerm);

	outputColor /= maxIntensity;
}